apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'sonar-runner'

group = 'com.github.agiledon'
version = '0.1-SNAPSHOT'
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral();
}

project.ext {
    guavaVersion = '15.0'
    slf4jVersion = '1.7.5'
    junitVersion = '4.11'
}

dependencies {
    compile(
            "com.google.guava:guava:$guavaVersion",
            "org.slf4j:slf4j-api:$slf4jVersion"
    )

    testCompile(
            "junit:junit:$junitVersion"
    )
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name project.name
                packaging 'jar'
                description 'Swift is smart library to support java collection'
                url 'https://github.com/agiledon/swift'

                scm {
                    url 'https://github.com/agiledon/swift'
                    connection 'https://github.com/agiledon/swift.git'
                    developerConnection 'git@github.com:agiledon/swift.git'
                }

                licenses {
                    license {
                        name 'MIT Licence'
                        url 'https://raw.github.com/agiledon/swift/master/MIT-LICENSE.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'agiledon'
                        name 'Zhang Yi'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

tasks.withType(Compile) {
    options.compilerArgs << "-Xlint:unchecked"
}